exists($$QPEDIR/src/build/trace_on):message(license.prf)
# License handling

PREV_LICENSE=
PROCESSED_LICENSE=
LICENSE_REQ=

for(l,forever) {
    for(lic,QTOPIA_LICENSE) {
        contains(PROCESSED_LICENSE,$$lic):next()
        PROCESSED_LICENSE+=$$lic
        
        equals(lic,COMMERCIAL) {
            # Commercial code can't be linked with GPL code.
        } else:equals(lic,LGPL) {
            # LGPL projects should be built as .so libs, even in singleexec mode.
            CONFIG-=enable_singleexec
            CONFIG-=singleexec
            CONFIG-=staticlib
            CONFIG+=shared
            # LGPL is GPL compatible
            QTOPIA_LICENSE*=GPL_COMPATIBLE
        } else:equals(lic,GPL) {
            # GPL stuff cannot work in singleexec mode.
            CONFIG+=no_singleexec
            # The GPL infects everything that uses it. Stop non-GPL projects from
            # linking to GPL projects.
            LICENSE_REQ+="contains(QTOPIA_LICENSE,GPL_COMPATIBLE)"
            LICENSE_REQ+="!contains(QTOPIA_LICENSE,NO_GPL)"
            !free_package:plugin:LICENSE_REQ+="contains(QTOPIA_LICENSE,GPL_LINK_EXCEPTION)"
        } else:equals(lic,GPL_COMPATIBLE) {
            # This is something that can be added to make a project GPL compatible.
        } else:equals(lic,GPL_LINK_EXCEPTION) {
            # This is something that must be added for a GPL-licensed plugin to work with a non-GPL system.
        } else:equals(lic,NO_GPL) {
            # This is something that can be added to explicitly make something not GPL compatible.
        } else:equals(lic,BSD) {
            # The BSD License is not GPL-compatible.
            # If the modern (GPL-compatible) version of the BSD license is used
            # the license line should be license(BSD GPL_COMPATIBLE)
        } else:equals(lic,OpenSSL) {
            # The OpenSSL license is not GPL-compatible.
            # GPL projects must grant an exception in order to link with OpenSSL.
            contains(QTOPIA_LICENSE,GPL):LICENSE_REQ+="contains(QTOPIA_LICENSE,OpenSSL_COMPATIBLE)"
        } else:equals(lic,OpenSSL_COMPATIBLE) {
            # This is something that can be added to make a GPL project OpenSSL-compatible.
        } else:equals(lic,FREEWARE) {
            # Freeware projects are implicitly GPL-compatible
            QTOPIA_LICENSE*=GPL_COMPATIBLE
        } else {
            error($$self: Unknown LICENSE $$lic)
        }

    }
    equals(QTOPIA_LICENSE,$$PREV_LICENSE):break()
    PREV_LICENSE=$$QTOPIA_LICENSE
}

for(c,LICENSE_REQ) {
    contains(QMAKE_BEHAVIORS,keep_quotes) {
        c~=s/^"//
        c~=s/"$//
    }
    echo(lic: $$c)
    requries($$c)
}

